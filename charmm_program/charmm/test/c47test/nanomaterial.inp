* testcase for autogeneration of image angles and dihedrals. -- January, 2022 -- BRB
* originally GENERATED BY CHARMM-GUI (http://www.charmm-gui.org) v2.0 on Dec, 11. 2018
* GENERATE NANOMATERIAL STRUCTURE AND COORDINATES
*

DIMENS CHSIZE 3000000 MAXRES 3000000

if ?domdec .eq. 1 then
   echo "Test NOT performed. Test incompatible with DOMDEC."
   stop
endif

STREam datadir.def
set 8  data/nanomaterial/

! Read topology and parameter files
stream @8toppar.str



!set impatopt   NOAUto NOREmd
!set impatopt   NOAUto NOREmd SORT
!set impatopt   NOAUto REMDupl
set impatopt   AUTO   REMDupl


!
! User Input
!
stream @8user_input.str
calc pi = 4.0 * atan (1.0) 

!
! Start with rectangle, then remove to shape
! Requires determining Lx/Ly/Lz for all cases
!
set type = @{material}@{milidx}

!
!shape determination
!
set XTLtype = triclinic

set shape = cube

if impatch .eq. yes set shape = slab

if rsphere .ne. 0 then
   set shape = sphere
   set Rsphere = @Rsphere
   calc Lx   = @Rsphere * 2 + 5
   calc Ly   = @Rsphere * 2 + 5
   calc Lz   = @Rsphere * 2 + 5
endif
if rcylinder .ne. 0 then
   set shape = cylinder
   set Rcylinder = @rcylinder
   calc Ly   = @Rcylinder * 2
   calc Lz   = @Rcylinder * 2
   if nanorod .eq. 1 then
      calc Lx = @Lx + @Rcylinder * 2
   endif
endif
if l_pgon .ne. 0 then
   set l_pgon = @{l_pgon}
   calc Lx = @lx
   calc Ly = @{l_pgon} * 2
   calc Lz = @{l_pgon} * 2
endif
  
set Lx   = @Lx
set Ly   = @Ly
set Lz   = @Lz

!
! reading unit cell
!

stream @8nanomodels/@type_unitcell.str

rename segid raw select segid @type end

define junk sele .not. init show end
if ?nsel .gt. 0 stop

calc Nx = int ( @Lx / @A ) + 1
calc Ny = int ( @Ly / @B ) + 1
calc Nz = int ( @Lz / @C ) + 1
calc orinx = int ( @Nx / 2 ) * @A
calc oriny = int ( @Ny / 2 ) * @B
calc orinz = int ( @Nz / 2 ) * @C

if wulff .eq. on then
   calc Nx = int ( @Nx / 2 + 1 )  * 2
   calc Ny = int ( @Ny / 2 + 1 )  * 2
   calc Nz = int ( @Nz / 2 + 1 )  * 2
   calc orinx = ( @Nx / 2 ) * @A
   calc oriny = ( @Ny / 2 ) * @B
   calc orinz = ( @Nz / 2 ) * @C
endif

!
! building nanomaterial crystal
! how many unit cells are there along X/Y/Z?
!
autogen off
calc Nxy = @Nx * @Ny
set iz = 1
label doz
   calc izp = ( @iz - 1 ) * @Nxy
   calc Lz = ( @iz - 1 ) * @C

    set ix = 1
    label dox
       calc ixyp = @izp + ( @ix - 1 ) * @Ny
       calc Lx = ( @ix - 1 ) * @A

        set iy = 1
        label doy
            calc Ly = ( @iy - 1 ) * @B
            calc isum = @ixyp + @iy
            if isum .eq. 1 then
               generate @{type} duplicate raw setup
               coor duplicate select segid raw end select segid @{type} end
            endif
            if isum .gt. 1 then
               generate temp duplicate raw setup
               coor duplicate select segid raw end select segid temp end
               coor stat sele segid temp end
               join @{type} ?SELSEGI renumber
            endif

            ! nanomaterial specific patch(es)
            stream @8nanomodels/@type_crystal.str  
      increase iy by 1
      if iy .le. @Ny goto doy

   increase ix by 1
   if ix .le. @Nx goto dox

increase iz by 1
if iz .le. @Nz goto doz

!
!delete raw unitcell structure
!
delete atom sele segid raw end
!open write card unit 10 name @9step1_1_@type.psf ! 
!write psf unit 10 card
!
!open write card unit 10 name @9step1_1_@type.crd ! 
!write coor unit 10 card
!
!open write card unit 10 name @9step1_1@type.pdb ! 
!write coor unit 10 pdb official 

!image patch based on periodicity

if impatch .eq. yes then
   ! Setup PBC (Periodic Boundary Condition)
   calc boxx = @Nx * @A
   calc boxy = @Ny * @B
   calc boxz = @Nz * @C
   crystal define @XTLtype  @boxx  @boxy  @boxz @alpha @beta @gamma 
   open unit 10 read card name @8crystal_image.str
   crystal read unit 10 card
   open read unit 10 card name @8cubic.img
   read imag unit 10
   print images tran
   update imall
endif

if impatch .eq. no goto skipimpatch

set iz = 1
label doimpz
   calc izp = ( @iz - 1 ) * @Nxy

   set ix = 1
   label doimpx
      calc ixyp = @izp + ( @ix - 1 ) * @NY  

      set iy = 1
      label doimpy
         calc isum = @ixyp + @iy
         ! image patch based on periodicity
         stream @8nanomodels/@type_impatch.str
      increase iy by 1
      if iy .le. @Ny goto doimpy

   increase ix by 1
   if ix .le. @Nx goto doimpx

increase iz by 1
if iz .le. @Nz goto doimpz

label skipimpatch


open write card unit 10 name @9image.psf
write image psf unit 10 
close unit 10


open write card unit 10 name @9step1_2_@type.psf ! 
write psf unit 10 card

open write card unit 10 name @9step1_2_@type.crd ! 
write coor unit 10 card

print psf full 
print image psf naming


!
!energy test
!

nbonds atom vatom vfswitch -
       ctonnb 10.0 ctofnb 12.0 cutnb 16.0 cutim 16.0 -
       inbfrq -1 imgfrq -1 wmin 1.0 cdie eps 1.0 -
 
energy
mini SD   nstep 50 nprint 5
mini ABNR nstep 50 nprint 5

open write card unit 10 name @9mini.crd !
write coor unit 10 card




