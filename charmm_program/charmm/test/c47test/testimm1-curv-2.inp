* CHARMM 47 testcase testimm1curv.inp
* Themis Lazaridis, Nov-21
* Example file for IMM1-curv: Magainin on a spherical anionic vesicle
*

if ?aspener .ne. 1 then
 echo "Test NOT performed."
 stop
endif

!if ?numnode .ne. 1 then
! echo "Test NOT performed in parallel."
! stop
!endif

stream datadir.def

open read card unit 3 name @0toph19_eef1.1.inp
read rtf unit 3 card
close unit 3

open read card unit 3 name @0param19_eef1.1.inp
read para unit 3 card
close unit 3

open read unit 3 card name @0testmaga.crd
read seque coor unit 3
close unit 3

generate main setup

open read unit 2 card name @0testmaga.crd
read coor card unit 2
close unit 2

coor orient

set tol 0.01

!inside a 100-A vesicle
coor trans zdir 88.7

eef1 setup membrane slvt water slv2 chex nsmth 10 width 26.0 temp 298.15 -
              unit 93 name @0solvpar.inp  aemp 0.85 -
     gouy anfr 0.3 area 70. conc 0.1 offset 3.0 valence 1 radu 100
update ctonnb 7. ctofnb 9. cutnb 10. group rdie

energy

!This is the correct answer:
!ENER>        0   -579.67039      0.00000      1.22444
!ENER INTERN>        3.07361     20.51597      0.00000      9.75739      2.66124
!ENER EXTERN>     -121.39552   -367.41015      0.00000   -126.87292      0.00000

@qcheck ?ENER -579.67 @tol testimm1-energy-inside-100A-vesicle

!inside a 100-A tube

eef1 setup membrane slvt water slv2 chex nsmth 10 width 26.0 temp 298.15 -
              unit 93 name @0solvpar.inp  aemp 0.85 -
     gouy anfr 0.3 area 70. conc 0.1 offset 3.0 valence 1 radu 100 tube

energy

!This is the correct answer:
!ENER>        0   -582.17264      2.50224      1.22613
!ENER INTERN>        3.07361     20.51597      0.00000      9.75739      2.66124
!ENER EXTERN>     -121.39552   -366.23815      0.00000   -130.54716      0.00000

@qcheck ?ENER -582.17 @tol testimm1-energy-inside-100A-tube

!outside a 100-A vesicle
coor trans zdir 24

eef1 setup membrane slvt water slv2 chex nsmth 10 width 26.0 temp 298.15 -
              unit 93 name @0solvpar.inp  aemp 0.85 -
     gouy anfr 0.3 area 70. conc 0.1 offset 3.0 valence 1 radu 100

energy

!This is the correct answer:
!ENER>        0   -614.66589     34.99550      1.20767
!ENER INTERN>        3.07361     20.51597      0.00000      9.75739      2.66124
!ENER EXTERN>     -121.39552   -354.89439      0.00000   -174.38418      0.00000

@qcheck ?ENER -614.66589 @tol testimm1-energy-outside-100A-vesicle

! Now with the GC approximation
eef1 setup membrane slvt water slv2 chex nsmth 10 width 26.0 temp 298.15 -
              unit 93 name @0solvpar.inp  aemp 0.85 -
     gouy anfr 0.3 area 70. conc 0.1 offset 3.0 valence 1 radu 100 GCCU

energy

!This is the correct answer:
!ENER>        0   -614.69833      0.03244      1.20725
!ENER INTERN>        3.07361     20.51597      0.00000      9.75739      2.66124
!ENER EXTERN>     -121.39552   -354.89439      0.00000   -174.41661      0.00000

@qcheck ?ENER -614.69833 @tol testimm1-gc-approx

stop
