*  Benchmark dynamics for DHFR in a cubic box of
*  solvent using PBC w/ PME.  Total system contains 23,558 atoms,
*  21,069 atoms associated with water molecules and 2589 protein atoms.
*

if ?blade .ne. 1 then
   echo "Test NOT performed. BLADE must be defined in pref.dat"
   stop
endif

if ?numnode .ne. 1 then
  echo "Test NOT performed in parallel."
  stop
endif

stream datadir.def

open unit 1 read form name @{0}top_all22_prot.inp
read rtf card unit 1
close unit 1

! NOTE: if using CMAP terms, parameters must be loaded with FLEX
open unit 1 read form name @{0}par_all22_prot.inp
read param card unit 1
close unit 1

open unit 1 read form name @{0}5dfr_minimized.crd
read sequ coor unit 1
close unit 1

generate 5dfr setup first nter last cter

read sequ tip3 7023
generate wat setup noangl nodihe

read coor pdb resi name @{0}5dfr_solv-cube_equil.pdb

faster on

! Dimension of a box
set 7 62.23

set theta 90.0
Crystal define cubic @7 @7 @7 @theta @theta @theta
crystal build cutoff 11 noper 0

image byseg xcen 0.0 ycen 0.0 zcen 0.0 select segid 5dfr end
image byres xcen 0.0 ycen 0.0 zcen 0.0 select segid wat end

!  turn on faster options and set-up SHAKE
shake fast bonh tol 1.0e-8 para

set nsteps 100
calc nprint = @nsteps / 10

open unit 1 write form name @{9}/blade.res
! Run NVE ensemble
! Note: BLaDE prints kinetic energy from the previous half time step, and thus appears not to conserve energy.
scalar fbeta set 0 sele all end
dynamics leap start timestep 0.002 nstep @nsteps nprint @nprint iprfrq @nprint -
     iseed 123456 nsavc 0 nsavv 0 -
     firstt 298 finalt 298 - ! twindl -5.0 twindh 5.0 -
     iunwri 1 isvfrq @nprint -
     eps 1.0 cutnb 11 cutim 11 ctofnb 9 ctonnb 7.5 vfswi -
     Ewald kappa 0.320 pmEwald order 4 fftx 64 ffty 64 fftz 64 ntrfq 0 - !PME
     blade

open unit 1 write form name @{9}/blade.res
open unit 2 write unform name @{9}/blade.dcd

! restart
dynamics leap restart timestep 0.002 nstep @nsteps nprint @nprint iprfrq @nprint -
     iseed 123456 nsavc @nprint nsavv 0 -
     firstt 298 finalt 298 - ! twindl -5.0 twindh 5.0 -
     iunwri 1 isvfrq @nprint iunrea 1 iuncrd 2 -
     eps 1.0 cutnb 11 cutim 11 ctofnb 9 ctonnb 7.5 vfswi -
     Ewald kappa 0.320 pmEwald order 4 fftx 64 ffty 64 fftz 64 ntrfq 0 - !PME
     blade


open unit 1 write form name @{9}/blade.res
! Run NVT ensemble
scalar fbeta set 0.1 sele all end
dynamics leap start timestep 0.002 nstep @nsteps -
     nprint @nprint iprfrq @nprint -
     iseed 123456 nsavc 0 nsavv 0 -
     firstt 298 finalt 298 - ! twindl -5.0 twindh 5.0 -
     iunwri 1 isvfrq @nprint -
     eps 1.0 cutnb 11 cutim 11 ctofnb 9 ctonnb 7.5 vfswi -
     Ewald kappa 0.320 pmEwald order 4 fftx 64 ffty 64 fftz 64 ntrfq 0 - !PME
     blade

open unit 1 write form name @{9}/blade.res
open unit 2 write unform name @{9}/blade.dcd

! restart
dynamics leap restart timestep 0.002 nstep @nsteps -
     nprint @nprint iprfrq @nprint -
     iseed 123456 nsavc @nprint nsavv 0 -
     firstt 298 finalt 298 - ! twindl -5.0 twindh 5.0 -
     iunwri 1 isvfrq @nprint iunrea 1 iuncrd 2 -
     eps 1.0 cutnb 11 cutim 11 ctofnb 9 ctonnb 7.5 vfswi -
     Ewald kappa 0.320 pmEwald order 4 fftx 64 ffty 64 fftz 64 ntrfq 0 - !PME
     blade

open unit 1 write form name @{9}/blade.res
! Run NPT ensemble
scalar fbeta set 0.1 sele all end
dynamics leap start timestep 0.002 nstep @nsteps -
     nprint @nprint iprfrq @nprint -
     iseed 123456 nsavc 0 nsavv 0 -
     firstt 298 finalt 298 - ! twindl -5.0 twindh 5.0 -
     iunwri 1 isvfrq @nprint -
     eps 1.0 cutnb 11 cutim 11 ctofnb 9 ctonnb 7.5 vfswi -
     Ewald kappa 0.320 pmEwald order 4 fftx 64 ffty 64 fftz 64 ntrfq 0 - !PME
     prmc -  ! use pressure monte carlo
     iprs 10 - ! Monte carlo pressure frequency (50 is default)
     pref 1 - ! reference pressure (atmospheres)
     prdv 100 - ! RMS proposed volume change (A cubed)
     blade

open unit 1 write form name @{9}/blade.res
open unit 2 write unform name @{9}/blade.dcd

! restart
dynamics leap restart timestep 0.002 nstep @nsteps -
     nprint @nprint iprfrq @nprint -
     iseed 123456 nsavc @nprint nsavv 0 -
     firstt 298 finalt 298 - ! twindl -5.0 twindh 5.0 -
     iunwri 1 isvfrq @nprint iunrea 1 iuncrd 2 -
     eps 1.0 cutnb 11 cutim 11 ctofnb 9 ctonnb 7.5 vfswi -
     Ewald kappa 0.320 pmEwald order 4 fftx 64 ffty 64 fftz 64 ntrfq 0 - !PME
     prmc -  ! use pressure monte carlo
     iprs 10 - ! Monte carlo pressure frequency (50 is default)
     pref 1 - ! reference pressure (atmospheres)
     prdv 100 - ! RMS proposed volume change (A cubed)
     blade

stop
