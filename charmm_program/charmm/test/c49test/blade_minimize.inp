* Test case for CHARMM/BLaDE interface minimizer
* Created by Ryan Hayes
* Adapted from CHARMM/OpenMM minimizer by Charles Brooks
* 2024-12-26
*

if ?blade .ne. 1 then
   echo "Test NOT performed. BLADE must be defined in pref.dat"
   stop
endif

if ?numnode .ne. 1 then
 echo "Test NOT performed in parallel."
 stop
endif

stream datadir.def

! read the topology and parameter files
read rtf card name @0/top_all36_prot.rtf
read param flex card name @0/par_all36_prot.prm ! blade requires flex parameters

read sequ ala 2
generate dala first ace last ct3 setup
ic param
ic seed 1 cay 1 cy 1 n
ic build

! Dimension of a box - blade requires periodic boundary conditions
set a 40.0
set theta 90.0
crystal define cubic @a @a @a @theta @theta @theta
open unit 1 read form name @0/cubic.xtl
crystal read card unit 1
close unit 1

scalar x store 1
scalar y store 2
scalar z store 3
coor copy comp
nbonds cutnb 12 ctofnb 10 ctonnb 8 fswitch vfswitch
update ! adding energy call requires update call for inexplicable reasons

!**** TEST1
energy blade
set ebefore = ?ener
prnlev 0
mini blade sd nstep 20 step 0.005
prnlev 5
energy
set eafter = ?ener
calc diff = @eafter - @ebefore
set passed1 = 1
if @diff .gt. 0 set passed1 = 0
if @passed1 eq 1 then
   echo testcase blade_minimize TEST1: pass
else
   echo testcase blade_minimize TEST1: fail energy diff @diff is greater than 0
endif

!**** TEST2
scalar x recall 1
scalar y recall 2
scalar z recall 3
coor copy comp

energy blade
set ebefore = ?ener
prnlev 0
mini blade sdfd nstep 20 step 0.005
prnlev 5
energy
set eafter = ?ener
calc diff = @eafter - @ebefore
set passed2 = 1
if @diff .gt. 0 set passed1 = 0
if @passed2 eq 1 then
   echo testcase blade_minimize TEST2: pass
else
   echo testcase blade_minimize TEST2: fail energy diff @diff is greater than 0
endif

calc passed = @passed1 * @passed2

if @passed eq 1 then
   echo testcase blade_minimize: pass
else
   echo testcase blade_minimize: fail
endif
stop
